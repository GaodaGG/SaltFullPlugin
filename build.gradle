plugins {
    id 'java-library'
    id 'org.jetbrains.kotlin.jvm' version '2.0.21'
    id 'org.jetbrains.kotlin.kapt' version '2.0.21'
}

group = 'com.gg.saltPlayerPlugin'
version = '1.0.0'

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

repositories {
    mavenCentral()
    maven {
        url = 'https://jitpack.io'
    }
}

dependencies {
//    implementation "com.code-disaster.steamworks4j:steamworks4j:1.9.0"
    // https://mvnrepository.com/artifact/net.java.dev.jna/jna-platform
    implementation 'net.java.dev.jna:jna-platform:5.17.0'
    compileOnly 'net.java.dev.jna:jna:5.17.0'

    compileOnly "org.jetbrains.kotlin:kotlin-stdlib:2.0.21"
    compileOnly "com.github.Moriafly:spw-workshop-api:0.1.0-dev08"
    kapt "com.github.Moriafly:spw-workshop-api:0.1.0-dev08"

    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}

def pluginClass = "com.gg.SaltFullPlugin.MainPlugin"
def pluginId = "全屏化SPW窗口"
def pluginVersion = "0.0.8"
def pluginProvider = "GaodaGG"

tasks.named("jar") {
    destinationDirectory = file(System.getenv("APPDATA") + "/Salt Player for Windows/workshop/")
    archiveFileName = "plugin-${pluginId}-${pluginVersion}.jar"

    manifest {
        attributes["Plugin-Class"] = pluginClass
        attributes["Plugin-Id"] = pluginId
        attributes["Plugin-Version"] = pluginVersion
        attributes["Plugin-Provider"] = pluginProvider
    }
}

tasks.register("plugin", Jar) {
    destinationDirectory = file(System.getenv("APPDATA") + "/Salt Player for Windows/workshop/")
    archiveFileName = "plugin-${pluginId}-${pluginVersion}.zip"
    archiveBaseName.set("plugin-" + pluginId + "-" + pluginVersion)

    into("classes") {
        with(tasks.named("jar").get())
    }
    dependsOn(configurations.runtimeClasspath)
    into("lib") {
        from({
            configurations.runtimeClasspath
                    .filter { it.name.endsWith("jar") }
        })
    }
    archiveExtension = 'zip'
}